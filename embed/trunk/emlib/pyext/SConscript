Import('env', 'modconfig','pythoninc','configure_for_pyext')

# Get a modified build environment suitable for building Python extensions:
e = configure_for_pyext(env, modconfig)
e.Append(CPPPATH=[pythoninc, '..'])
e.Append(LIBPATH=['..'], LIBS='emlib')

# AIX tries to use the C compiler rather than g++, so hardcode it here:
shlink = str(e['SHLINK'])
e['SHLINK'] = shlink.replace(' cc_r', ' g++')


# Build the Python extension from SWIG interface file:
pyext = e.SharedLibrary('_emlib', 'emlib.i',
                        SWIGFLAGS='-python -lpointer.i -c++ ')
# .py file should also be generated:
pyem = File('emlib.py')
e.Depends(pyem, 'emlib.i')

# Install the Python extension and any .py files:
libinst = e.Install(modconfig['libinstall'], pyext)
pyinst = e.Install(modconfig['pyinstall'], pyem)
e.Alias('install', [libinst, pyinst])

Return('pyext')
