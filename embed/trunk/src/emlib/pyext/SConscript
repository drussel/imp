Import('env', 'get_pyext_environment')

# Get a modified build environment suitable for building Python extensions:
e = get_pyext_environment(env, cplusplus=True)
e.Append(CPPPATH=['..'])
e.Append(LIBPATH=['..'], LIBS='em')



# Build the Python extension from SWIG interface file:
pyext = e.LoadableModule('_EM', 'em.i',
                        SWIGFLAGS='-python -c++ -naturalvar -IEMLIB')
# .py file should also be generated:
pyem = File('em.py')
e.Depends(pyem, 'em.i')

# Install the Python extension and any .py files:
libinst = e.Install(e['pyextdir'], pyext)
pyinst = e.Install(e['pythondir'], pyem)
e.Alias('install', [libinst, pyinst])

Return('pyext')
