Import('env', 'get_sharedlib_environment')

# Get an environment suitable for building a shared library:
e = get_sharedlib_environment(env, 'IMPDOMINO_EXPORTS', cplusplus=True)

e.Append(CPPPATH=['#/kernel/include', e['BOOST_CPPPATH']])
e.Append(LIBPATH=['#/build/libs'], LIBS=['imp'])

# Generate exports header and version information
e.IMPModuleExports('domino_exports.h', e.Value('domino'))
ver_cpp, ver_h = e.IMPModuleVersionInfo(('domino_version_info.cpp',
                                         'domino_version_info.h'),
                                        (e.Value('domino'),
                                         e.Value('Keren Lasker'),
                                         e.Value('SVN')))

files = ('CombState.cpp', 'JNode.cpp', 'JEdge.cpp', 'RestraintGraph.cpp',
         'DominoOptimizer.cpp', 'SimpleDiscreteRestraint.cpp', ver_cpp)

# Build the shared library:
lib = e.SharedLibrary('#/build/libs/imp_domino', files)
e.Alias('domino', lib)

# Install the library:
libinst = e.Install(e['libdir'], lib)
e.Alias('domino-install', [libinst])

Return('lib')
