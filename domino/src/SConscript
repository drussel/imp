Import('env', 'get_sharedlib_environment')

# Get an environment suitable for building a shared library:
e = get_sharedlib_environment(env, 'DOMINO_EXPORTS', cplusplus=True)

e.Append(CPPPATH=['#/kernel/include', e['BOOST_CPPPATH']])
e.Append(LIBPATH=['#/build/libs'], LIBS=['imp'])

# Generate exports header
e.IMPModuleExports('domino_exports.h', e.Value('domino'))

files = ('CombState.cpp', 'JNode.cpp', 'JEdge.cpp', 'RestraintGraph.cpp',
         'DominoOptimizer.cpp', 'SimpleDiscreteRestraint.cpp')

# Build the shared library:
lib = e.SharedLibrary('#/build/libs/imp_domino', files)

# Install the library:
libinst = e.Install(e['libdir'], lib)
e.Alias('domino-install', [libinst])

Return('lib')
