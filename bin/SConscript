Import('env')
import sys

# Custom builder to generate file:
def builder_script_file(target, source, env):
    infile = file(source[0].abspath, 'r')
    outfile = file(target[0].abspath, 'w')

    sep=":"
    varname=None
    if env['PLATFORM'] == 'posix' or env['PLATFORM']=='sunos':
        varname= "LD_LIBRARY_PATH"
    elif env['PLATFORM'] == 'win32':
        sep=";"

    for line in infile:
        line = line.rstrip('\r\n')
        if line == "@LDPATH@":
            if varname:
                libs = [source[1].get_contents() + '/build/lib']
                libpath = source[4].get_contents()
                if libpath and not env['rpath']:
                    libs.append(libpath)
                line = varname + "='" + ':'.join(libs) + "'\nexport " + varname
            else:
                line=""
        if line == "@PYTHONPATH@":
            line= "PYTHONPATH='"+source[1].get_contents()+"/build/lib"+sep+source[2].get_contents().replace(":", sep)+"'\nexport PYTHONPATH"
        if line == "@MODPY@":
            if len(source[3].get_contents())!=0:
                line = "modeller_init_script='" + source[3].get_contents()+"'"
            else:
                line=""
        print >> outfile, line
    outfile.close()
    infile.close()
    env.Execute(Chmod(str(target[0]), 0755))

env.Append(BUILDERS = {'ScriptFile': Builder(action=builder_script_file)})

bin = env.ScriptFile("imppy.sh",
                     ["imppy.sh.in", env.Value(env.Dir('#').abspath),
                      env.Value(env.get('pythonpath', "")),
                      env.Value(env['MODELLER_MODPY']),
                      env.Value(':'.join(env['libpath']))])

env.AlwaysBuild("imppy.sh")

Return('bin')
