Import('env')
import scons_tools.dependency
import scons_tools.dependency.boost
import scons_tools.dependency.gcc
import scons_tools.dependency.range_for
import scons_tools.dependency.enums

config=[]

if not env.GetOption('help'):
    if env['IMP_PASS']=="BUILD":
        scons_tools.dependency.range_for.configure_check(env)
        scons_tools.dependency.enums.configure_check(env)

    if not env.get('deprecated', True):
        config.append(['IMP_USE_DEPRECATED', 1])
    else:
        config.append(['IMP_USE_DEPRECATED', 1])

    if env['versionchecks']:
        if env.get("BOOST_VERSION", None):
            config.append(["IMP_BOOST_VERSION", env["BOOST_VERSION"]])

    if env['build'] == 'fast':
        config.append(["IMP_BUILD", "IMP_FAST"])
    elif env['build'] == 'release':
        config.append(["IMP_BUILD", "IMP_RELEASE"])
    elif env['build'] == 'debug' or env['build']=='compile':
        config.append(["IMP_BUILD", "IMP_DEBUG"])

# could use env.Command
env.Execute("%s --output=\"%s\" --datapath=\"%s\" --examplepath=\"%s\""\
            %(File("#/scons_tools/build_tools/setup_paths.py").abspath,
              File("#/build/src/base/paths.cpp").abspath, env['datadir'],
              env['docdir']+"/examples"))


deplist=[]

scons_tools.dependency.boost.add_boost_library(env, "FileSystem", "filesystem",
                                               'boost/filesystem/path.hpp',
                                               body="boost::filesystem::path p;",
                                               extra_boost_libs=["system"])
scons_tools.dependency.boost.add_boost_library(env, "ProgramOptions", "program_options",
                                               'boost/program_options.hpp')

env.IMPModuleBuild(config_macros=config)
