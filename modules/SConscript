Import('env')

# modules must be listed after modules that use them
modules=['cgal',
         'algebra',
         'core',
         'container',
         'atom',
         'display',
         'domino',
         'em',
         'example',
         'misc',
         'saxs',
         'helper',
         'statistics',
         'gsl',
         'modeller',
         'restrainer',
         'em2d',
         'domino2',
         'multifit',
         'bullet']
# always last
modules.append('benchmark')

disabled=env['disabledmodules'].split(":")

def skip(m):
    env[m+"_ok"]=False
    env[m+"_required_modules"]=[]

for p in modules:
    if p in disabled:
        skip(p)
    else:
        SConscript(p+"/SConscript")

if env.get('localmodules', False):
    for d in Glob("*/SConscript"):
        p= str(d).split("/")[0]
        if p in modules:
            pass
        elif p in disabled:
            skip(p)
        else:
            SConscript(p+"/SConscript")
