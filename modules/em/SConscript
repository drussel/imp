from tools import embed

Import('env')
Export('env')

env.IMPModuleAbout(author='Keren Lasker', version='SVN',
                   description='Interface to the EMBED package.')

# Modify our environment for building against EMBED:
embed.configure_check(env)

# Make sure that all builds with this environment fail if EMBED was not found:
if env['EM_LIBS'] == '' or env['MODELLER_LIBS'] == '':
    env.invalidate(embed.fail)
else:
    env.validate()

env.Append(CPPPATH=env['EM_CPPPATH'], SWIGPATH=env['EM_CPPPATH'])
env.Append(LIBPATH=env['EM_LIBPATH'], LIBS=[env['EM_LIBS'], 'imp_core',
                                            'imp_algebra'])

# _USE_MATH_DEFINES is needed to get math.h to define M_LN2 under w32
env.Append(CPPDEFINES='_USE_MATH_DEFINES')

# Subdirectories:
SConscript('src/SConscript')
SConscript('include/SConscript')
SConscript('pyext/SConscript')
SConscript('test/SConscript')
