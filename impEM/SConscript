from tools import emlib

Import('env', 'get_sharedlib_environment', 'invalidate_environment')

Help("""
Type: 'scons impEM' to build and test the impEM extension module;
      'scons impEM-install' to install it.
""")

# Get custom environment for building against EMLIB:
emenv = env.Clone()
emlib.configure_check(emenv)
Export('emenv')

# Make sure that all builds with this environment fail if EMLIB was not found:
if emenv['EM_LIBS'] == '' or emenv['MODELLER_LIBS'] == '':
    invalidate_environment(emenv, emlib.fail)

# Get an environment suitable for building a shared library:
e = get_sharedlib_environment(emenv, 'IMPEM_EXPORTS', cplusplus=True)

e.Append(CPPPATH=['#/kernel/include', e['EM_CPPPATH']])
e.Append(LIBPATH=['#/kernel/src', e['EM_LIBPATH']], LIBS=['imp', e['EM_LIBS']])

# _USE_MATH_DEFINES is needed to get math.h to define M_LN2 under w32
e.Append(CPPDEFINES='_USE_MATH_DEFINES')

files = ('IMPParticlesAccessPoint.cpp', 'EMFitRestraint.cpp')

# Build the shared library:
lib = e.SharedLibrary('impem', files)

# Install the library:
libinst = e.Install(e['libdir'], lib)
e.Alias('impEM-install', [libinst])

# Subdirectories:
pyext = SConscript('pyext/SConscript')
test = SConscript('test/SConscript')
e.Depends(test, [pyext, lib])

Return('lib')
