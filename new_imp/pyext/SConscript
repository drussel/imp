import os.path

Import('env', 'get_pyext_environment')

# Move src to dest, but only if src exists:
from SCons.Action import ActionFactory
def _move_action(dest, src):
    if os.path.exists(src):
        os.rename(src, dest)
MoveIfExists = \
  ActionFactory(_move_action,
                lambda dest, src: 'MoveIfExists("%s", "%s")' % (dest, src))


# Get a modified build environment suitable for building Python extensions:
e = get_pyext_environment(env, cplusplus=True)
e.Append(CPPPATH='../IMP')
e.Append(LIBPATH='../IMP', LIBS='imp')

# Build the Python extension from SWIG interface file:
pyext = e.LoadableModule('_IMP', 'IMP.i',
                         SWIGFLAGS='-python -c++ -naturalvar -IIMP')
# .py file should also be generated; move to IMP subdirectory:
e.AddPostAction(pyext, MoveIfExists('pyext/IMP/__init__.py', 'pyext/IMP.py'))
pymod = File('IMP/__init__.py')
e.Depends(pymod, pyext)

# Install the Python extension:
libinst = e.Install(e['pyextdir'], pyext)
e.Alias('install', libinst)

# Subdirectories
SConscript('IMP/SConscript')

Return('pyext')
