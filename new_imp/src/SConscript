Import('env', 'get_sharedlib_environment')

# Get an environment suitable for building a shared library:
env = get_sharedlib_environment(env, 'IMP_EXPORTS', cplusplus=True)
env.Append(CPPPATH="#/include")

# Subdirectories:
restraints_files = SConscript('restraints/SConscript')
optimizers_files = SConscript('optimizers/SConscript')
decorators_files = SConscript('decorators/SConscript')

# Source files
files = ['base_types.cpp', 'ScoreFunc.cpp', 'Model.cpp', 'ModelData.cpp',
         'Particle.cpp', 'RigidBody.cpp', 'ScoreState.cpp', 'Log.cpp',
         'Restraint.cpp', 'Optimizer.cpp', 'Object.cpp'
        ] + decorators_files + restraints_files + optimizers_files

# Build the shared library:
lib = env.SharedLibrary('imp', files)

# Install the library:
libinst = env.Install(env['libdir'], lib)
env.Alias('install', [libinst])

Return('lib')
