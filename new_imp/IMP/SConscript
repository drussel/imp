Import('env', 'modconfig', 'pythoninc', 'configure_for_pyext',
       'is_wine_platform')

# Get a modified build environment suitable for building Python extensions:
e = configure_for_pyext(env, modconfig)
e.Append(CPPPATH=[pythoninc])

# Source files
files = ('Score_Func.cpp', 'Model.cpp', 'Model_Data.cpp', 'Optimizer.cpp',
         'Particle.cpp', 'Restraint_Set.cpp', 'Restraint.cpp',
         'Complexes_Restraints.cpp', 'Rigid_Body.cpp', 'log.cpp', 'imp.cpp')

# Build the Python extension from SWIG interface file:
lib = e.SharedLibrary('_imp2', files + ('imp.i',),
                      SWIGFLAGS='-python -c++ -naturalvar')

# Install the Python extension and any .py files:
libinst = e.Install(modconfig['libinstall'], lib)
e.Alias('install', [libinst])
Return('lib')
