Import('env', 'modconfig', 'pythoninc', 'configure_for_pyext')

# Get a modified build environment suitable for building Python extensions:
e = configure_for_pyext(env, modconfig)
e.Append(CPPPATH=[pythoninc, '..'])
e.Append(LIBPATH=['..'], LIBS='imp')

# AIX tries to use the C compiler rather than g++, so hardcode it here:
shlink = str(e['SHLINK'])
e['SHLINK'] = shlink.replace(' cc_r', ' g++')

# Build the Python extension from SWIG interface file:
pyext = e.SharedLibrary('_imp2', 'imp.i',
                        SWIGFLAGS='-python -c++ -naturalvar -IIMP')
# .py file should also be generated:
pymod = File('imp2.py')
e.Depends(pymod, 'imp.i')

# Install the Python extension and any .py files:
libinst = e.Install(modconfig['libinstall'], pyext)
pyinst = e.Install(modconfig['pyinstall'], pymod)
e.Alias('install', [libinst, pyinst])

Return('pyext')
