Import('env', 'bin')

import os

e = env.Copy()

# Custom builder to run unit tests:
def builder_unit_test(target, source, env):
    (dir, script) = os.path.split(source[0].abspath)
    app = "cd %s && %s %s %s -v > /dev/null" % (dir, source[1].abspath,
                                                e['PYTHON'], script)
    if env.Execute(app) == 0:
        file(str(target[0]), 'w').write('PASSED\n')
    else:
        print "unit tests FAILED"
        return 1
e.Append(BUILDERS = {'Test': Builder(action=builder_unit_test)})

# All tests:
test = e.Test("test.passed", ["run-all-tests.py", bin])
e.Alias("test", test)
e.AlwaysBuild("test.passed")

# Only IMP (no Modeller) tests:
test = e.Test("imptest.passed", ["run-imp-tests.py", bin])
e.Alias("imptest", test)
e.AlwaysBuild("imptest.passed")
