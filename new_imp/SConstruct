# Include IMP build utility functions:
import sys
sys.path.append('../')
from tools import *

# Set up build environment:
opts = Options()
add_common_options(opts, "imp")
env = MyEnvironment(options=opts, require_modeller=False,
                    tools=["default", "doxygen"],
                    toolpath=["../tools"])
Help(opts.GenerateHelpText(env))

# Some targets require MODELLER:
if 'test' in COMMAND_LINE_TARGETS and env['MODELLER_EXETYPE'] == '':
    print "ERROR: cannot run full test suite without MODELLER."
    print "You could try the 'imptest' target instead for just"
    print "the non-MODELLER tests."
    Exit(1)

# Make these objects available to SConscript files:
Export('env', 'get_pyext_environment', 'get_sharedlib_environment')

# Subdirectories to build:
bin = SConscript('bin/SConscript')
Export('bin')
src = SConscript('IMP/SConscript')
pyext = SConscript('pyext/SConscript')
SConscript('rsr/IMP_mov/SConscript')
SConscript('doc/SConscript')

# bin script first requires libraries to be built:
env.Depends(bin, [src, pyext])

# Build the binaries by default:
env.Default(bin)
