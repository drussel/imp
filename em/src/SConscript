Import('emenv', 'get_sharedlib_environment', 'ver_cpp')

# Get an environment suitable for building a shared library:
e = get_sharedlib_environment(emenv, 'IMPEM_EXPORTS', cplusplus=True)

e.Append(CPPPATH=['#/build/include', e['EM_CPPPATH'], e['BOOST_CPPPATH']])
e.Append(LIBPATH=['#/build/libs', e['EM_LIBPATH']], LIBS=['imp', e['EM_LIBS']])

# _USE_MATH_DEFINES is needed to get math.h to define M_LN2 under w32
e.Append(CPPDEFINES='_USE_MATH_DEFINES')

files = ('IMPParticlesAccessPoint.cpp', 'EMFitRestraint.cpp', ver_cpp)

# Build the shared library:
lib = e.SharedLibrary('#/build/libs/imp_em', files)

# Install the library:
libinst = e.Install(e['libdir'], lib)
e.Alias('em-install', [libinst])

Return('lib')
