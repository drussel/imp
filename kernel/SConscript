Import('env')
import os.path

env.Append(CPPDEFINES=["IMP_DATA_DIRECTORY=\\\""+os.path.join(env['datadir'],"IMP")+"\\\""])

config=[]
if env['CGAL_LIBS'] != ['']:
    config.append('IMP_USE_CGAL')
else:
    config.append('IMP_NO_CGAL')
if not env.get('deprecated', True):
    config.append('IMP_NO_DEPRECATED')
else:
    config.append('IMP_USE_DEPRECATED')
if env['IMP_ENDIAN']=='little':
    config.append("IMP_LITTLE_ENDIAN")
else:
    config.append('IMP_BIG_ENDIAN')

config.append(["IMP_DEBUG", "0"])
config.append(["IMP_RELEASE", "1"])
config.append(["IMP_FAST", "2"])

if env['build'] == 'fast':
    config.append(["IMP_BUILD", "IMP_FAST"])
elif env['build'] == 'release':
    config.append(["IMP_BUILD", "IMP_RELEASE"])
elif env['build'] == 'debug':
    config.append(["IMP_BUILD", "IMP_DEBUG"])


env.IMPModuleBuild(version='SVN', required_modules=None, config_macros=config,
                   module='kernel', module_suffix="", module_include_path="IMP",
                   module_src_path="kernel", module_preproc="IMP", module_namespace="IMP")
