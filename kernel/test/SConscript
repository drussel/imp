Import('env', 'bin')

import os

e = env.Clone()

# Custom builder to run unit tests:
def builder_unit_test(target, source, env):
    (dir, script) = os.path.split(source[0].abspath)
    app = "cd %s && %s %s %s -v > /dev/null" % (dir, source[1].abspath,
                                                e['PYTHON'], script)
    if env.Execute(app) == 0:
        file(str(target[0]), 'w').write('PASSED\n')
    else:
        print "unit tests FAILED"
        return 1

def fail_modeller(env, target, source):
    print "\n  ERROR: cannot run full test suite without MODELLER."
    print "  You could try the 'imptest' target instead for just"
    print "  the non-MODELLER tests.\n"
    return 1

e.Append(BUILDERS = {'Test': Builder(action=builder_unit_test)})

# All tests (require Modeller):
source = ["run-all-tests.py", bin]
if e['MODELLER_EXETYPE'] != '':
    test = e.Test("test.passed", source)
else:
    test = e.Command("test.passed", source, fail_modeller)
e.Alias("test", test)
e.AlwaysBuild("test.passed")

# Only IMP (no Modeller) tests:
imptest = e.Test("imptest.passed", ["run-imp-tests.py", bin])
e.Alias("imptest", imptest)
e.AlwaysBuild("imptest.passed")

Return('test', 'imptest')
